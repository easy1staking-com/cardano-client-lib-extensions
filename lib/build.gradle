plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {

    api('com.bloxbean.cardano:cardano-client-lib:0.7.0-beta4')

    api('com.bloxbean.cardano:yaci-store-utxo:0.1.4')

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Use JUnit Jupiter for testing.
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'cardano-client-lib-extensions'
            from components.java
            
            pom {
                name = 'Cardano Client Library Extensions'
                description = 'Extensions and utilities for the Cardano Client Library'
                url = 'https://github.com/easy1staking-com/cardano-client-lib-extensions'
                
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                
                developers {
                    developer {
                        id = 'easy1staking'
                        name = 'Easy1Staking'
                        email = 'contact@easy1staking.com'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/easy1staking-com/cardano-client-lib-extensions.git'
                    developerConnection = 'scm:git:ssh://github.com/easy1staking-com/cardano-client-lib-extensions.git'
                    url = 'https://github.com/easy1staking-com/cardano-client-lib-extensions'
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "CentralPortal"
            url = "https://central.sonatype.com/api/v1/publisher/upload?publishingType=USER_MANAGED"
            credentials {
                username = project.findProperty("centralPortalUsername") ?: System.getenv("CENTRAL_PORTAL_USERNAME")
                password = project.findProperty("centralPortalPassword") ?: System.getenv("CENTRAL_PORTAL_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
